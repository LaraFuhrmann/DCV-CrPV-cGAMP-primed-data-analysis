from snakemake.utils import Paramspace
import yaml

def load_config(path):
    with open(path, 'r') as stream:
        config = yaml.safe_load(stream)
    return config

import pandas as pd

#config = load_config("config/config.yaml")
configfile: "config/config.yaml"

"""
module vpipe_crpv_p0:
    snakefile: github("cbg-ethz/V-pipe", path="workflow/Snakefile", tag="v3.0.0.pre0")  # "../resources/V-pipe/workflow/Snakefile"
    config: config["vpipe_crpv_p0"]
use rule * from vpipe_crpv_p0 as vpipe_crpv_p0_*
"""
module vpipe_crpv:
    snakefile: github("cbg-ethz/V-pipe", path="workflow/Snakefile", tag="v3.0.0.pre0")  # "../resources/V-pipe/workflow/Snakefile"
    config: config["vpipe_crpv"]
use rule * from vpipe_crpv as vpipe_crpv_*

rule all:
    input:
        #rules.vpipe_crpv_p0_all.input,
        rules.vpipe_crpv_all.input,
        "results/crpv_coverage.csv",
        "results/crpv_all_mutations.csv",
        "results/crpv_all_mutations_lofreq.csv",
        "results/crpv_all_mutations.annotated.csv",
        #f"results/all_mutations.annotated.csv",
        #f"results/crpv_all_consensus.fasta",
        #f"results/dcv_all_consensus.fasta",
        #f"results/snpgenie_codon_results.csv",
        #f"results/snpgenie_population_summary.csv",
        #f"results/snpgenie_product_results.csv",
        #f"results/snpgenie_site_results.csv",
    default_target: True

datasets = vpipe_crpv.datasets + ['results/crpv/parental_p0']


rule collect_consensus_sequences:
    input:
        fnames_consensus = expand("{dataset}/references/ref_majority.fasta", dataset=datasets),
    output:
        fname_merged = "results/dcv_all_consensus.fasta",
    conda:
        "envs/annotate_vcf.yaml"
    script:
        "./scripts/collect_consensus_sequences.py"

rule run_lofreq:
    input:
        fname_bam="{dataset}/alignments/REF_aln.bam",
        fname_reference_dcv=config['vpipe_dcv']['input']['reference'],
        fname_reference_crpv=config['vpipe_crpv']['input']['reference'],
        fname_reference_p0_dcv=config['viloca']['parental_dcv'],
        fname_reference__p0_crpv=config['viloca']['parental_crpv'],
    output:
        fname_snv_vcf="{dataset}/variants_lofreq/snvs.vcf",
        dname_work=directory(
            "{dataset}/variants_lofreq/"
            ),
    conda:
        "envs/lofreq.yaml"
    script:
        "scripts/lofreq.py"


rule run_viloca:
    input:
        fname_bam="{dataset}/alignments/REF_aln.bam",
        fname_reference_dcv=config['vpipe_dcv']['input']['reference'],
        fname_reference_crpv=config['vpipe_crpv']['input']['reference'],
        fname_reference_p0_dcv=config['viloca']['parental_dcv'],
        fname_reference__p0_crpv=config['viloca']['parental_crpv'],
    params:
        window_size=54,
    output:
        fname_snv_vcf="{dataset}/variants/SNVs/snv/SNVs_0.010000_final.vcf",
        fname_snv_csv="{dataset}/variants/SNVs/snv/SNVs_0.010000_final.csv",
        fname_csv="{dataset}/variants/SNVs/SNVs_0.010000_final.csv",
        dname_work=directory(
            "{dataset}/variants/SNVs/"
            ),
    conda:
        "envs/viloca.yaml"
    resources:
        mem_mb=80000,
        runtime=48*60,
    threads: 40
    script:
        "scripts/run_viloca.py"

rule annotate_vcf:
    input:
        fname_snvs_vcf=rules.run_viloca.output.fname_snv_vcf,
        fname_genbank_file="GCF_000853145.1_ViralProj14832_genomic_crpv.gbff",
    output:
        fname_snvs_vcf="{dataset}/snvs.annotated.vcf",
    conda:
        "envs/annotate_vcf.yaml"
    script:
        "./scripts/annotate_vcf.py"

rule collect_annotated_mutations:
    input:
        fnames_snv_csv=expand("{dataset}/snvs.annotated.vcf", dataset=datasets),
    output:
        fname_all_mutations="results/crpv_all_mutations.annotated.csv",
    conda:
        "envs/annotate_vcf.yaml"
    script:
        "./scripts/collect_annotated_mutations.py"

rule collect_all_mutations:
    input:
        fnames_snv_csv=expand("{dataset}/variants/SNVs/snv/SNVs_0.010000_final.csv", dataset=datasets),
    output:
        fname_all_mutations="results/crpv_all_mutations.csv",
    conda:
        "envs/annotate_vcf.yaml"
    script:
        "./scripts/collect_mutations.py"

use rule collect_all_mutations as lofreq_collect_all_mutations with:
    input:
        fnames_snv_csv=expand("{dataset}/variants_lofreq/snvs.vcf", dataset=datasets),
    output:
        fname_all_mutations="results/crpv_all_mutations_lofreq.csv",



rule collect_coverage:
    input:
        fnames_coverage=expand("{dataset}/alignments/coverage.tsv.gz", dataset=datasets),
    output:
        fname_coverage="results/crpv_coverage.csv",
        fname_read_len="results/crpv_read_len.csv",
    conda:
        "envs/annotate_vcf.yaml"
    script:
        "./scripts/collect_coverage.py"

rule run_snpgenie:
    input:
        fname_snvs_vcf="{dataset}/variants/SNVs/snvs.vcf",
        fname_reference=config['vpipe_crpv']['input']['reference'],
        fname_gtffile="resources/references/GCF_000853145.1_ViralProj14832_genomic_crpv.gtf",
    output:
        fname_codon="{dataset}/variants/SNPGenie_Results_work/codon_results.txt",
        fname_population_summary="{dataset}/variants/SNPGenie_Results_work/population_summary.txt",
        fname_product_results="{dataset}/variants/SNPGenie_Results_work/product_results.txt",
        fname_site_results="{dataset}/variants/SNPGenie_Results_work/site_results.txt",
        dname_work=directory("{dataset}/variants/SNPGenie_Results_work/"),
    conda:
        "envs/snpgenie.yaml"
    script:
        "./scripts/run_snpgenie.py"

rule collect_snpgenie:
    input:
        fname_codon=expand("{dataset}/variants/SNPGenie_Results_work/codon_results.txt", dataset=vpipe_crpv.datasets),
        fname_population_summary=expand("{dataset}/variants/SNPGenie_Results_work/population_summary.txt", dataset=vpipe_crpv.datasets),
        fname_product_results=expand("{dataset}/variants/SNPGenie_Results_work/product_results.txt", dataset=vpipe_crpv.datasets),
        fname_site_results=expand("{dataset}/variants/SNPGenie_Results_work/site_results.txt", dataset=vpipe_crpv.datasets),
    output:
        fname_codon=f"results/crpv_snpgenie_codon_results.csv",
        fname_population_summary=f"results/crpv_snpgenie_population_summary.csv",
        fname_product_results=f"results/crpv_snpgenie_product_results.csv",
        fname_site_results=f"results/crpv_snpgenie_site_results.csv",
    conda:
        "envs/snpgenie.yaml"
    script:
        "./scripts/collect_snpgenie_results.py"
